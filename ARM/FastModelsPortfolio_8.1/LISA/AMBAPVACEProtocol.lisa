/*
 * AMBAPVACEProtocol.lisa - AMBA-PV ACE protocol definition.
 *
 * Copyright 2011-2013 ARM Limited.
 * All rights reserved.
 */

protocol AMBAPVACE {
    includes {
        #include <amba_pv.h>
    }
    properties {
        version = "8.1.37";
        description = "AMBA-PV ACE protocol";
        sc_master_base_class_name = "amba_pv::amba_pv_ace_master_base";
        sc_master_socket_class_name = "amba_pv::amba_pv_ace_master_socket<64>";
        sc_slave_base_class_name = "amba_pv::amba_pv_slave_base<64>";
        sc_slave_socket_class_name = "amba_pv::amba_pv_ace_slave_socket<64>";
        dso_safe = 0;
        documentation_file = "../Docs/DUI0423O_fast_model_rm.pdf";
    }

    /* Slave behaviours for the AMBA-PV ACE protocol */

    /* Blocking transport. */
    slave
    behaviour b_transport(int socket_id,
                          amba_pv::amba_pv_transaction & trans,
                          sc_core::sc_time & t);

    /* Debug access to a slave. */
    slave optional
    behaviour transport_dbg(int socket_id,
                            amba_pv::amba_pv_transaction & trans): unsigned int {
        return 0;
    };

    /* Requests a DMI access. */
    slave optional
    behaviour get_direct_mem_ptr(int socket_id,
                                 amba_pv::amba_pv_transaction & trans,
                                 tlm::tlm_dmi & dmi_data) : bool {
        dmi_data.allow_read_write();
        dmi_data.set_start_address(0x0);
        dmi_data.set_end_address(sc_dt::uint64(-1));
        return false;
    };

    /* Master behaviours for the AMBA-PV ACE protocol */

    /* Blocking snoop. */
    master optional
    behaviour b_snoop(int socket_id,
                     amba_pv::amba_pv_transaction & trans,
                     sc_core::sc_time & t) {
    };

    /* Snoop debug access to a master. */
    master optional
    behaviour snoop_dbg(int socket_id,
                        amba_pv::amba_pv_transaction & trans) : unsigned int {
       return 0;
    };

    /* Invalidates a DMI access. */
    master optional
    behaviour invalidate_direct_mem_ptr(int socket_id,
                                        sc_dt::uint64 start_range,
                                        sc_dt::uint64 end_range) {
    };
}
