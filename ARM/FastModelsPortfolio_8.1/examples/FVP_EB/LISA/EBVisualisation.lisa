/*
 * Copyright 2007-2009 ARM Limited. All rights reserved.
 */

// Demonstration of using the Visualisation library to create a skinnable
// display window.

component EBVisualisation
{
    properties
    {
        version = "8.1.37";
        component_type = "Peripheral";
        description = "Display window for EB using Visualisation library.";
        documentation_file = "../../../#DUI0423#";
    }
    includes
    {
        #include "sg/SystemInclude.h"
        #include "sg/MSCFixes.h"
        #include "UIClass/UIClass.h"

        #define EBVIS_TRACE if (!0) ; else printf

        #ifdef WIN32
            #define pvis_sleep(x) Sleep(x)
        #else
            #include <sys/time.h>
            #define pvis_sleep(x) usleep((x)*1000)
        #endif
    }

    resources
    {
        // An instance of the Visualisation library.
        Visualisation *vis;

        // LCD display region.
        VisRenderRegion *render_lcd;

        // One display region for status information.
        VisRenderRegion *render_status;

        // Current configured size of the LCD.
        unsigned int lcd_width;
        unsigned int lcd_height;

        // Maps keycode to a button
        // unused - should be done in newer SDLVis
        // MxU32 key2button[KC_MAX+1];

        Clabel        *user_dips_label;      // User dipswitch label
        Cdipswitch    *user_dips;            // User dipswitch
        Clabel        *boot_dips_label;      // Boot dipswitch label
        Cdipswitch    *boot_dips;            // Boot dipswitch
        Clabel        *led_label;            // LEDs label
        Cled          *user_led;             // LEDs
        Clabel        *daughter_label;       // Daughterboard LEDs/dipswitch label
        Cdipswitch    *daughter_dips;        // Daughterboard dipswitch
        Cled          *daughter_led;         // Daughterboard LEDs
        Clabel        *rl_label;             // Rate Limit label
        Cbutton       *rl_button;            // Rate Limit button
        Cicount       *UIcount1;             // Instruction count
        Clabel        *label_TI;             // Total Instr
        Clabel        *label_IPS;            // Instruction per Second
        Clabel        *label_time_perf;      // Label text for total time or performance index
        Clabel        *value_time_perf;      // Value text for total time or performance index
        Clabel        *mouse_label;          // Grab/Release mouse text
        Cseparateline *white_line;           // White divider

        // Current configured height of the status area.
        int status_height;

        // Cursor position, for rendering parts of the status area.
        int sx, sy;

        // Remember where we put total instruction and total time region so we can detect clicks on them
        int instr_region_x, instr_region_y, instr_region_w, instr_region_h;

        // True if the window layout needs to be reconfigured on the
        // next screen redraw.
        bool relayout_needed;

        // status of the ctrl key
        bool ctrl_key;

        // toggle mouse
        bool toggle_mouse;

        // current absolute mouse position (to calc relative changes)
        int mouse_abs_x, mouse_abs_y;

        bool    disp_totals;
        bool    disp_delay;
        bool    stopped;

        // Used by the performance calculation routines
        // Outputs
        uint64_t sim_ips;
        float    sim_pidx;

        // Intermediate values
        uint32_t start_time;          // Aggr. sim time calculated on the fly
        uint32_t last_time;
        uint64_t last_ticks;
        uint32_t perf_index_real_time_ms;     // accumulated real time elapsed between calculateStats() (in ms)
        uint32_t perf_index_sim_time_ms;      // accumulated sim  time elapsed between calculateStats() (in ms)
        uint32_t throttle_last_real_time_ms;  // last absolte real time timer::timer_callback_handler.signal() was called (for throttling, in ms)
        // time in ms that simulator is ahead of real time (this is the budget we can sleep from)
        // (this does the whole averaging magic and compensation for sleep() inaccuracies)
        // (x<0 == sim time is x ms behind real time, x>0 sim time is x ms ahead of real time)
        int throttle_budget;
        int calculate_stats_phase;

        VisRasterLayout* fake_layout;  // fake area for pl11x when visualisation is disabled

        PARAMETER { description("Trap key that works with left Ctrl to toggle mouse display."), type(uint32_t), default(74) }  trap_key; // 74 = KC_LALT = Left Alt (press Left Ctrl plus Left Alt to lock/unlock mouse)
        PARAMETER { name("rate_limit-enable"), description("Rate limit simulation."), type(bool), default("true") } rate_limit_enable;
        PARAMETER { description("Enable/disable visualisation"), type(bool), default("false") } disable_visualisation;
        PARAMETER { description("Window title (%cpu% is replaced by cpu_name)"), type(string), default("Fast Models - CLCD %cpu%") } window_title;

        PARAMETER { description("Cpu name displayed in window title"), type(string), default("") } cpu_name;

        PARAMETER { name("daughter_led_count"), description("Number of LEDs that the daughter board has"),
                    type(uint32_t), default(0), max(32), min(0) } number_of_daughter_leds;
        PARAMETER { name("daughter_user_switch_count"), description("Number of switches that the daughter board has"),
                    type(uint32_t), default(0), max(32), min(0) } number_of_daughter_user_switches;

        uint32_t daughter_user_switches_value;
        uint32_t daughter_leds_value;

        bool     redraw_daughter_switches;

    }

    composition
    {
        // Visualisation subcomponent responsible for generating real-time callbacks.
        idler : GUIPoll(delay_ms = 20);

        // Timer for perf calculation
        timer : ClockTimer;

        // event recorder functionality
        recorder: VisEventRecorder;
    }

    internal slave  port <TimerCallback> timer_callback_handler
    {
        // called every 100 simulated ms
        behavior signal() : uint32_t
        {
            // get simulated time slice length in ms
            int sim_ms_per_tick = 100; // 100ms

            // get real time slice length in ms
            uint32_t ct = get_msTime();
            int real_ms_per_tick = ct - throttle_last_real_time_ms;
            throttle_last_real_time_ms = ct;

            // update performance index nominator and denominator
            perf_index_sim_time_ms += sim_ms_per_tick;
            perf_index_real_time_ms += real_ms_per_tick;

            // do not sleep when throttling is disabled
            uint32_t timer_ticks_per_callback = 5; // 100ms
            if (!disp_delay)
                return timer_ticks_per_callback;

            // limit real time passed to one second to avoid extreme cases
            if (real_ms_per_tick > 1000)
                real_ms_per_tick = 1000;

            // update time budget (the ms we have been too fast recently)
            throttle_budget += sim_ms_per_tick;
            throttle_budget -= real_ms_per_tick;

            // limit time budget to catch up for at most 1 second
            // (we can catch up at most 1 second of simulation time during slow simulation speed by running faster than real time)
            // (this gets rid of all short term performance drops while not allowing the simulation to catch up indedinitely)
            if (throttle_budget < -1000)
                throttle_budget = -1000;

            // limit time budget to at most 500ms of sleep
            // (this would never occur if the OS sleep() is 100% accurate, but it isn't so this is an emergency exit:
            // never wait for more than one second no matter what we calculated above, in practice throttle_budget
            // will always be below 100 (ms))
            if (throttle_budget > 500)
                throttle_budget = 500;

            // if our time budget is positive: sleep, if it is negavtive or 0: continue as fast as possible
            if (throttle_budget > 0)
                pvis_sleep(throttle_budget);

            // call us back next clock tick (100ms == 10Hz simulated time)
            return timer_ticks_per_callback;
        }
    }
    slave port<ClockSignal> clock_50Hz;

    master port<MouseStatus> touch_screen;

    master port<LCDLayoutInfo> lcd_layout;

    // called every 20ms by GUIPoll
    behavior calculateStats(bool force)
    {
        // every 100ms
        calculate_stats_phase++;
        if ((calculate_stats_phase < 5) && (!force))
            return;
        calculate_stats_phase = 0;

        // update leds more often than the rest
        draw_leds();
        if (redraw_daughter_switches)
        {
            draw_switches();
        }

        uint32_t ct = get_msTime(); // don't rely on polled time
        uint32_t elapsed_time_ms = ct - last_time;

        // update stats at most every 500ms
        if ((elapsed_time_ms < 500) && (!force))
            return;

        uint64_t ic = ticks.getValue();

        // only update IPS when time has passed - to avoid divide by zero:
        if (elapsed_time_ms)
        {
            // calculate ips here to get a decent sample period
            sim_ips = (ic - last_ticks);
            sim_ips = (((uint64_t)1000) * sim_ips) / elapsed_time_ms;
        }

        // calculate performance index (1.0 == real time, < 1.0 == sim is running too slow, > 1.0 sim is running too fast)
        if (perf_index_real_time_ms)
            sim_pidx  = float(perf_index_sim_time_ms) / perf_index_real_time_ms;
        perf_index_sim_time_ms = 0;
        perf_index_real_time_ms = 0;

        if (last_ticks == ic) {
            start_time += elapsed_time_ms;
            sim_pidx = 0.0;
        }

        last_ticks = ic;
        last_time  = ct;

        draw_status(ct, ic);
    }

    connection
    {
        idler.gui_callback => self.gui_callback;

        self.clock_50Hz => timer.clk_in;
        timer.timer_callback => self.timer_callback_handler;
        self.recorderControl => recorder.control;
        self.ticks => recorder.ticks;
    }

    // Handle real-time callback events from the GUIPoll subcomponent.
    internal slave port<GUIPollCallback> gui_callback
    {
        behavior gui_callback()
        {
            if (disable_visualisation)
                return;
            processMessages(false);
            calculateStats(false);
        }
    }


    // Implement the PL110 FrameBuffer protocol for each display.
    slave port<LCD> lcd
    {
        behavior lock() : const VisRasterLayout *
        {
            if (disable_visualisation)
                return fake_layout;
            return render_lcd->lock();
        }
        behavior unlock()
        {
            if (disable_visualisation)
                return;
            return render_lcd->unlock();
        }
        behavior update(int x, int y, unsigned int w, unsigned int h)
        {
            if (disable_visualisation)
                return;
            if (lcd_layout.setLayoutInfo.implemented())
                lcd_layout.setLayoutInfo(x, y, w, h); // Send LCD layout info to touch screen
            return render_lcd->update(x, y, w, h);
        }
        behavior setPreferredLayout(unsigned int w, unsigned int h, unsigned int d)
        {
            if (disable_visualisation)
                return;
            // Ignore requested depth - request 24-bits.
            (void)d;
            resize_lcd(w, h);
        }
    }

    slave port<InstructionCount> ticks;

    slave port<ValueState>       leds;
    slave port<ValueState>       daughter_leds
    {
        behaviour setValue( uint32_t value_ )
        {
            // Note no need to redraw as the GUI pulse will redraw it.
            daughter_leds_value = value_;
        }

        behaviour getValue() : uint32_t
        {
            return daughter_leds_value;
        }
    }
    slave port<ValueState>       daughter_user_switches
    {
        behaviour setValue( uint32_t value_ )
        {
            daughter_user_switches_value = value_;
            // Trigger the redraw of the switches in a normal GUI pulse.
            redraw_daughter_switches = true;
        }

        behaviour getValue() : uint32_t
        {
            return daughter_user_switches_value;
        }
    }
    slave port<ValueState>       user_switches;
    slave port<ValueState>       boot_switch;

    // Send keypress events to a PS2Keyboard device.
    master port<KeyboardStatus>  keyboard;

    // Send mouse events to a PS2Mouse device.
    master port<MouseStatus>     mouse;

    behavior init()
    {
        composition.init(); // Pass the call down to subcomponents.

        //
        // It is _very_ important to set these to zero first otherwise we
        // try freeing something we never allocated.
        //

        user_dips_label    = 0;
        user_dips          = 0;
        boot_dips_label    = 0;
        boot_dips          = 0;
        led_label          = 0;
        user_led           = 0;
        daughter_label     = 0;
        daughter_dips      = 0;
        daughter_led       = 0;
        rl_label           = 0;
        rl_button          = 0;
        UIcount1           = 0;
        label_TI           = 0;
        label_IPS          = 0;
        label_time_perf    = 0;
        value_time_perf    = 0;
        mouse_label        = 0;
        white_line         = 0;

        if (disable_visualisation)
        {
            fake_layout = new VisRasterLayout();
            return;
        }

        ctrl_key = false;
        toggle_mouse = false;
        start_time     = get_msTime();
        last_time      = start_time;
        last_ticks     = ticks.getValue();
        perf_index_real_time_ms = 0;
        perf_index_sim_time_ms = 0;
        throttle_last_real_time_ms = start_time;
        throttle_budget = 0;
        calculate_stats_phase = 0;
        sim_pidx = 0.0;
        sim_ips  = 0;

        disp_delay  = rate_limit_enable;
        disp_totals = true;
        stopped     = false;

        daughter_user_switches_value = 0x0;
        redraw_daughter_switches = false;
        daughter_leds_value          = 0x0;

        lcd_width = 320;
        lcd_height = 240;
        init_gui();
    }

    behavior init_gui()
    {
        // Initialise the visualisation library and the display window.
        vis = createVisualisation(cpu_name.c_str(), window_title.c_str());

        // Create display regions for the LCD panel.
        render_lcd = vis->createRenderRegion();
        recorderControl.registerVisRegion(render_lcd, "render_lcd");

        render_status = vis->createRenderRegion();
        recorderControl.registerVisRegion(render_status, "render_status");

        resize_lcd(640, 40);

        reconfigureWindow();

        render_lcd->setVisible(true);
        render_status->setVisible(true);

        init_status();
    }

    behavior init_status()
    {
        // Black background
        render_status->fillRect(render_status->mapRGB(0, 0, 0),
                                0, 0, lcd_width, status_height);

        // Status region layout
        // First line

        delete user_dips_label;
        user_dips_label = new Clabel(render_status, "^ON USERSW 1..8", VIS_FONT_WIDTH, 2);

        // \todo If the user_switches port, etc. are not connected then the delegate
        //       will assert fail.
        delete user_dips;
        user_dips = new Cdipswitch(render_status,
                                   user_dips_label->getMaxX() + VIS_FONT_WIDTH*2, 1,
                                   8, user_switches.getValue());

        delete led_label;
        led_label = new Clabel(render_status, "S6LED0..7",
                               user_dips->getMaxX() + VIS_FONT_WIDTH*4, 2);


        delete daughter_label;
        if ( (number_of_daughter_leds != 0) || (number_of_daughter_user_switches !=0) )
        {
            daughter_label = new Clabel(render_status, "Daughter", 
                                        led_label->getMaxX() + VIS_FONT_WIDTH*4, 2);
        }

        delete daughter_dips;
        if (number_of_daughter_user_switches != 0)
        {
            daughter_dips = new Cdipswitch(render_status,
                                           daughter_label->getMaxX() + VIS_FONT_WIDTH, 1,
                                           number_of_daughter_user_switches,
                                           daughter_user_switches_value);
        }

        // Second line
        delete boot_dips_label;
        boot_dips_label = new Clabel(render_status, "^ON BOOTSW 1..8",
                                     user_dips_label->getX(), VIS_FONT_HEIGHT + 1);

        delete boot_dips;
        boot_dips = new Cdipswitch(render_status, user_dips->getX(), VIS_FONT_HEIGHT + 1,
                                   8, boot_switch.getValue.implemented() ? boot_switch.getValue() : 0);

        delete user_led;
        user_led = new Cled(render_status, led_label->getX(), VIS_FONT_HEIGHT + 2,
                            8, leds.getValue());

        delete daughter_led;
        if (number_of_daughter_leds != 0)
        {
            daughter_led = new Cled( render_status,
                                     daughter_label->getMaxX() + VIS_FONT_WIDTH, VIS_FONT_HEIGHT + 2,
                                     number_of_daughter_leds, daughter_leds_value);
        }

        // Third line
        unsigned col  = 1;
        instr_region_x = VIS_FONT_WIDTH;
        instr_region_y = 1 + VIS_FONT_HEIGHT*2;
        instr_region_w = VIS_FONT_WIDTH*(1+12+6+12+12+10);
        instr_region_h = VIS_FONT_HEIGHT;
        delete label_TI;
        label_TI  = new Clabel(render_status, "Total Instr:",
                               VIS_FONT_WIDTH*col, 1 + VIS_FONT_HEIGHT*2);
        delete label_IPS;
        label_IPS = new Clabel(render_status, "Instr / sec:",
                               VIS_FONT_WIDTH*col, 1 + VIS_FONT_HEIGHT*2);
        col += 12+1;

        delete UIcount1;
        UIcount1 = new Cicount(render_status,
                               VIS_FONT_WIDTH*col, 1 + VIS_FONT_HEIGHT*2);
        UIcount1->setLeftJustify();
        col += 12+5;

        delete label_time_perf;
        label_time_perf  = new Clabel(render_status, 12,
                               VIS_FONT_WIDTH*col, 1 + VIS_FONT_HEIGHT*2);
        col += 12+1;

        delete value_time_perf;
        value_time_perf = new Clabel(render_status, 10,
                               VIS_FONT_WIDTH*col, 1 + VIS_FONT_HEIGHT*2);
        value_time_perf->setLeftJustify();

        // Rate Limiting
        delete rl_label;
        rl_label = new Clabel(render_status, 14,
                              lcd_width - VIS_FONT_WIDTH*(14+1), 2);
        delete rl_button;
        rl_button = new Cbutton(render_status,
                                lcd_width - VIS_FONT_WIDTH - 15, VIS_FONT_HEIGHT,
                                15, 11, disp_delay, VIS_RED);

        // passive Grab/Release mouse text
        delete mouse_label;
        mouse_label = new Clabel(render_status, 31,
                                 lcd_width - VIS_FONT_WIDTH*(31+1), VIS_FONT_HEIGHT*2);
        mouse_label->setRightJustify();

        // White divider
        delete white_line;
        white_line = new Cseparateline(render_status, 0, status_height - 2, lcd_width, 1);

        draw_labels();
        draw_leds();
        draw_switches();
        draw_buttons();
        draw_divider();
        draw_status(get_msTime(), ticks.getValue());
    }

    behavior draw_buttons(void)
    {
        rl_button->setState(disp_delay);
    }

    behavior draw_leds(void)
    {
        if (leds.getValue.implemented())
            user_led->setState(leds.getValue());

        if (daughter_led != 0)
            daughter_led->setState(daughter_leds_value);
    }

    behavior draw_divider(void)
    {
        white_line->redraw();
    }

    behavior draw_labels(void)
    {
        if (disp_totals) {
            label_TI->redraw();
        } else {
            label_IPS->redraw();
        }
        label_time_perf->setLabel(disp_totals ? "Total Time: " : "Perf Index:");

        user_dips_label->redraw();
        boot_dips_label->redraw();
        led_label->redraw();
        if (daughter_label != 0)
        {
            daughter_label->redraw();
        }
        rl_label->setLabel(disp_delay ? "Rate Limit  ON" : "Rate Limit OFF" );
        mouse_label->setLabel(toggle_mouse ? "Release mouse: LeftCtrl+LeftAlt" :
                                             "Grab mouse: LeftCtrl+LeftAlt" );
    }

    behavior draw_switches(void)
    {
        if (user_switches.getValue.implemented())
            user_dips->setState(user_switches.getValue());

        if (daughter_dips != 0)
            daughter_dips->setState(daughter_user_switches_value);

        if (boot_switch.getValue.implemented())
            boot_dips->setState(boot_switch.getValue());

        redraw_daughter_switches = false;
    }

    behavior draw_status(uint32_t msTime, uint64_t ticks)
    {
        char buf[30];

        if (disp_totals) {
            UIcount1->setCount(ticks);
            value_time_perf->setLabel(ms2str(buf, msTime - start_time));
        } else {
            UIcount1->setCount(sim_ips);
            sprintf(buf,"%01.2f",sim_pidx);
            value_time_perf->setLabel(buf);
        }
        draw_switches();
        draw_labels();
    }

    behavior reset(int level)
    {
        composition.reset(level);
        if (disable_visualisation)
            return;

        timer.timer_control.cancel();
        timer.timer_control.set(5);

        start_time     = get_msTime();
        last_time      = start_time;
        last_ticks     = ticks.getValue();
        perf_index_real_time_ms = 0;
        perf_index_sim_time_ms = 0;
        throttle_last_real_time_ms = start_time;
        throttle_budget = 0;
        calculate_stats_phase = 0;

        sim_pidx = 0.0;
        sim_ips  = 0;
        mouse_abs_x = mouse_abs_y = 0;
    }

    behavior delete_gui_elements
    {
        if (disable_visualisation)
            return;
        delete user_dips_label;
        delete user_dips;
        delete boot_dips_label;
        delete boot_dips;
        delete led_label;
        delete user_led;
        delete daughter_label;
        delete daughter_dips;
        delete daughter_led;
        delete rl_label;
        delete rl_button;
        delete label_TI;
        delete label_IPS;
        delete UIcount1;
        delete label_time_perf;
        delete value_time_perf;
        delete mouse_label;
        delete white_line;

        delete render_status;
        delete render_lcd;

        delete vis;
    }

    behavior terminate()
    {
        delete_gui_elements();
        if (disable_visualisation)
            delete fake_layout;
        // Explicitly invoke subcomponent's terminate() behavior.
        composition.terminate();
    }

    behavior get_msTime(void) : uint32_t
    {
        #ifdef WIN32
        return (GetTickCount());
        #else
        struct timeval tv;
        if (gettimeofday(&tv, NULL) != 0) {
            return 0;
        }
        return (tv.tv_sec*1000)+(tv.tv_usec/1000);
        #endif
    }

    behavior toggleMouse(void)
    {
        toggle_mouse = !toggle_mouse;
        toggle_mouse?vis->lockMouse(render_lcd):vis->unlockMouse();
        draw_labels();
    }

    // Helper functions for processMessages
    behavior handleKeyDown(ATKeyCode key)
    {
        if ( key < 256 )
            keyboard.keyDown(key);

        if ( key == KC_LCONTROL )
            ctrl_key = true;

        if ( key == (ATKeyCode)trap_key )
            if (ctrl_key)
                toggleMouse();
    }

    behavior handleKeyUp(ATKeyCode key)
    {
        if (key < 256)
            keyboard.keyUp(key);

        if ( key == KC_LCONTROL )
            ctrl_key = false;
    }

    // process messages from and control VisEventRecorder subcomponent
    internal master port<VisEventRecorderProtocol> recorderControl
    {
        master behavior processEvents()
        {
            processMessages(true);
        }
    }

    // Process the GUI message loop
    behavior processMessages(bool playback)
    {
        VisEvent event;

        if (relayout_needed) {
            reconfigureWindow();
        }

        while (((!playback) && vis->poll(&event)) || (playback && recorderControl.getEvent(&event)))
        {
            recorderControl.putEvent(&event);
            switch(event.type)
            {
                case VisEvent::KEYDOWN:
                    handleKeyDown((ATKeyCode)event.keysym);
                    break;

                case VisEvent::KEYUP:
                    handleKeyUp((ATKeyCode)event.keysym);
                    break;

                case VisEvent::MOUSEBUTTONDOWN:
                    if (event.region == render_status) {
                        if (user_dips->isHit(event.abs_x, event.abs_y))
                        {
                            if (user_switches.setValue.implemented())
                                user_switches.setValue(user_dips->getState());
                        }
                        else if (daughter_dips != 0 &&
                                 daughter_dips->isHit(event.abs_x, event.abs_y))
                        {
                            daughter_user_switches_value = daughter_dips->getState();
                        }
                        else if (boot_dips->isHit(event.abs_x, event.abs_y))
                        {
                            if (boot_switch.setValue.implemented())
                                boot_switch.setValue(boot_dips->getState());
                        }
                        else if (ticks.getValue.implemented() &&
                            event.abs_x >= instr_region_x &&
                            event.abs_x < instr_region_x + instr_region_w &&
                            event.abs_y >= instr_region_y &&
                            event.abs_y < instr_region_y + instr_region_h)
                        {
                            disp_totals = !disp_totals;
                            draw_labels();
                            calculateStats(true);
                        }
                        else if (rl_button->isHit(event.abs_x, event.abs_y))
                        {
                            disp_delay = !disp_delay;
                            if (disp_delay)
                            {
                                // reset throttling mechanism
                                throttle_last_real_time_ms = get_msTime();
                                throttle_budget = 0;
                            }
                            draw_buttons();
                        }
                    }
                    if (event.region == render_lcd) {
                        emit_mouse_move(event);
                        if (mouse.mouseButton.implemented()) {
                            mouse.mouseButton(event.button, true);
                        }
                        if (touch_screen.mouseButton.implemented()) {
                            touch_screen.mouseButton(event.button, true);
                        }
                    }
                    break;
                case VisEvent::MOUSEBUTTONUP:
                    if (event.region == render_lcd) {
                        emit_mouse_move(event);
                        if (mouse.mouseButton.implemented()) {
                            mouse.mouseButton(event.button, false);
                        }
                        if (touch_screen.mouseButton.implemented())
                            touch_screen.mouseButton(event.button, false);
                    }
                    break;
                case VisEvent::MOUSEMOTION:
                    if (event.region == render_lcd) {
                        emit_mouse_move(event);
                    }
                    break;
                case VisEvent::QUIT:
                    // When the user clicks on close, this
                    // will quit the simulation if it's running
                    // headless, but just stop it if a debugger
                    // is connected.
                    if(!control_port.quit.empty())
                        control_port.quit();
                    break;
                case VisEvent::None: break;
            }
        }
    }

    // emit mouse move event on render_lcd
    behavior emit_mouse_move(const VisEvent &event)
    {
        // This functions does two non obvious things:
        // - emit mouse.mouseMove() with a maximum step size of 100
        //   (Some OSes can get out of sync when the step size is more than about 200.
        //   This is rather harmless, as such big step sizes only occur when the mouse leaves
        //   the LCD window and enters it again at a totally different location.)
        // - emit relative move events based on differences of absolute values emitted through this interface
        //   instead of the relative information in 'event'
        //   (The absolute coordinates implicitly capture also mouse moves in the 'render_status' area.
        //   The relative info in 'event' does not.)

        // calculate relative mouse movements
        int rel_x = event.abs_x - mouse_abs_x;
        int rel_y = event.abs_y - mouse_abs_y;
        mouse_abs_x = event.abs_x;
        mouse_abs_y = event.abs_y;

        // emit mouse.mouseMove() with a maximum step size of 100
        int step = 100; // The value doesn't matter much. Some OSes can't deal with steps bigger than roughly 200.
        if (mouse.mouseMove.implemented()) {
            for (; rel_x | rel_y; )
            {
                int step_x = rel_x;
                if (step_x < -step)
                    step_x = -step;
                else if (step_x > step)
                    step_x = step;
                int step_y = rel_y;
                if (step_y < -step)
                    step_y = -step;
                else if (step_y > step)
                    step_y = step;
                mouse.mouseMove(step_x, step_y);
                rel_x -= step_x;
                rel_y -= step_y;
            }
        }
        if (touch_screen.mouseMove.implemented()) {
            touch_screen.mouseMove(event.abs_x,event.abs_y);
        }
    }

    // LCD controller timing parameters have changed: attempt to adjust
    // display size to match.
    behavior resize_lcd(unsigned int w, unsigned int h)
    {
        if (w == lcd_width && h == lcd_height) {
            return;
        }
        status_height = VIS_FONT_HEIGHT*3 + 4;
        lcd_width = w;
        lcd_height = h;
        relayout_needed = true;
    }

    behavior reconfigureWindow()
    {
        relayout_needed = false;
        vis->configureWindow(lcd_width,
                             lcd_height + status_height,
                             24);
        render_lcd->setLocation(0, status_height, lcd_width, lcd_height);
        render_status->setLocation(0, 0, lcd_width, status_height);

        init_status();
    }

    behavior ms2str(char *buf, uint32_t ms) : char *
    {
        int s, m, h, d;
        s= ms/1000;
        d = s/(60*60*24);
        s = s-(d*60*60*24);
        h = s/(60*60);
        s = s-(h*60*60);
        m = s/60;
        s = s-(m*60);
        if (d) {
            sprintf(buf, "%dd, %d:%d:%d",d,h,m,s);
        } else if (h){
            sprintf(buf, "%2d:%02d:%02d",h,m,s);
        }  else if (m) {
            sprintf(buf, "%dm %02ds",m,s);
        }  else {
            sprintf(buf, "%ds",s);
        }
        return(buf);
    }
}
